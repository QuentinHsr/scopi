cmake_minimum_required(VERSION 3.0)

project(scopi)

# Flags
# ===========

set(CMAKE_SKIP_BUILD_RPATH FALSE)
set(CMAKE_BUILD_WITH_INSTALL_RPATH FALSE)

set(CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}/lib")
set(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)

set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -Wpedantic ")
set (CMAKE_CXX_STANDARD 17)
message(STATUS "CMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD}")

set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake")

set(SCOPI_INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/include)

# Versionning
# ===========

include(version)
set_version()

# Options
# =======
OPTION(SCOPI_USE_TBB "enable TBB" OFF)
OPTION(SCOPI_USE_OPENMP "enable OpenMP" OFF)
OPTION(SCOPI_USE_MOSEK "enable Mosek" OFF)
OPTION(SCOPI_USE_SCS "enable SCS" OFF)
OPTION(SCOPI_USE_MKL "enable MKL" OFF)
OPTION(BUILD_TESTS "scopi test suite" OFF)

if(SCOPI_USE_TBB AND SCOPI_USE_OPENMP)
    message(
        FATAL
        "SCOPI_USE_TBB and SCOPI_USE_OPENMP cannot both be active at once"
    )
endif()

# Dependencies
# ============
find_package(xtensor REQUIRED)
find_package(nlohmann_json REQUIRED)
find_package(fmt REQUIRED)
# find_package(nanoflann)

# section needed to use xtensor-blas
# see https://xtensor-blas.readthedocs.io/en/latest/performance.html
add_definitions(-DHAVE_CBLAS=1)

if (WIN32)
    find_package(OpenBLAS REQUIRED)
    set(BLAS_LIBRARIES ${CMAKE_INSTALL_PREFIX}${OpenBLAS_LIBRARIES})
else()
    find_package(BLAS REQUIRED)
    find_package(LAPACK REQUIRED)
endif()

message(STATUS "BLAS VENDOR:    " ${BLA_VENDOR})
message(STATUS "BLAS LIBRARIES: " ${BLAS_LIBRARIES})
# end of xtensor-blas section

if(SCOPI_USE_TBB)
    find_package(TBB REQUIRED)
endif()

if(SCOPI_USE_OPENMP)
    find_package(OpenMP REQUIRED)
endif()

if(SCOPI_USE_MKL)
    if(SCOPI_USE_TBB)
        set(MKL_THREAD_LAYER "TBB")
    endif()
    find_package(MKL REQUIRED)
endif()

if(SCOPI_USE_MOSEK)
    find_package(MOSEK REQUIRED)
    if(APPLE)
        set(MOSEK_FUSION_LIBRARY "" CACHE FILEPATH "MOSEK FUSION LIBRARY")
    endif(APPLE)
endif()

if(SCOPI_USE_SCS)
    find_package(scs REQUIRED)
endif()

# Package
# =======

set (CMAKE_NO_SYSTEM_FROM_IMPORTED TRUE)

add_library(scopi INTERFACE)

target_include_directories(scopi INTERFACE $<BUILD_INTERFACE:${SCOPI_INCLUDE_DIR}>
                                               $<INSTALL_INTERFACE:include>)

target_link_libraries(scopi INTERFACE xtensor
                                          nlohmann_json::nlohmann_json
                                          fmt::fmt
                                        #   nanoflann::nanoflann
                                          ${BLAS_LIBRARIES} ${LAPACK_LIBRARIES})

if(SCOPI_USE_MKL)
    target_link_libraries(scopi INTERFACE MKL::MKL)
    target_compile_definitions(scopi INTERFACE SCOPI_USE_MKL)
endif()

if(SCOPI_USE_TBB)
    include_directories(SYSTEM ${TBB_INCLUDE_DIRS})
    target_link_libraries(scopi INTERFACE ${TBB_LIBRARIES})
    target_compile_definitions(scopi INTERFACE SCOPI_USE_TBB)
endif()

if(SCOPI_USE_OPENMP)
    target_link_libraries(scopi INTERFACE OpenMP::OpenMP_CXX)
    target_compile_definitions(scopi INTERFACE SCOPI_USE_OPENMP)
endif()

if(SCOPI_USE_MOSEK)
    include_directories(SYSTEM ${MOSEK_INCLUDE_DIR})
    target_link_libraries(scopi INTERFACE ${MOSEK_LIBRARIES})
    target_compile_definitions(scopi INTERFACE SCOPI_USE_MOSEK)
endif()

add_subdirectory(demos EXCLUDE_FROM_ALL)
if(BUILD_TESTS)
    add_subdirectory(test)
endif()


# Installation
# ============

include(installation)
install_project()
